import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
    id 'idea'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.apache.commons:commons-configuration2:2.10.1'
    implementation 'org.apache.httpcomponents:httpclient:[4.5.11,4.6)'
    implementation 'org.opencadc:cadc-access-control-identity:[1.1.0,2.0)'
    implementation 'org.opencadc:cadc-dali:[1.2.6,1.3)'
    implementation 'org.opencadc:cadc-log:[1.1.4,1.2.0)'
    implementation 'org.opencadc:cadc-rest:[1.4.0,2.0)'
    implementation 'org.opencadc:cadc-util:[1.5.7,2.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.23,1.3)'
    implementation 'org.opencadc:cadc-web-util:[1.2.7,1.3.0)'
    implementation 'org.opencadc:caom2:[2.4.1, 2.5.0)'
    implementation 'org.opencadc:caom2-search-lib:[2.5.5,2.6.0)'
    implementation 'org.opencadc:jsky:[1.0,2.0)'
    implementation 'org.postgresql:postgresql:[42.3.1,43.0.0)'

    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    providedRuntime 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.2'
    providedRuntime 'org.apache.taglibs:taglibs-standard-impl:1.2.5'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.easymock:easymock:[5.6,6.0)'
    testImplementation 'org.opencadc:cadc-web-test:[2.1.3,3.0)'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    intTest
}

configurations {
    intTestCompile
    intTestCompile.extendsFrom(testCompile)
}

subprojects {
    plugins.withType('idea') {
        idea {
            module {
                // some extra test source dirs
                testSourceDirs += file('src/intTest/java')
                scopes.TEST.plus += [ configurations.intTestCompile ]
            }
        }
    }
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDirs = sourceSets.intTest.output.getClassesDirs()
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'org.opencadc:cadc-web-test:[2.1.2, )'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:3.+'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:3444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }
        else {
            println "Please set the intTest_web_app_url property (-PintTest_web_app_url=https://myhost.com...)."
        }

        if (project.hasProperty('intTest_user_name'))
        {
            systemProperty 'user.name', project.intTest_user_name
        }

        if (project.hasProperty('intTest_user_password'))
        {
            systemProperty 'user.password', project.intTest_user_password
        }
    }
}

static def getDate() {
    new Date().format('yyyy-MM-dd')
}

war {
    // Trying to fix static file caching on new build.
    eachFile {
        if (it.name.endsWith('.jsp')) {
            filter(ReplaceTokens, tokens: [version: project.version])
        }

        if (it.name.equals('index.jsp')) {
            filter(ReplaceTokens, tokens: ['LastChangedDate': getDate()])
        }
    }

    archiveFileName = 'AdvancedSearch.war'
}
